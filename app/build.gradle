import java.security.MessageDigest
import java.text.SimpleDateFormat


plugins {
    id "com.android.application"
}

// Android配置信息
android {

    signingConfigs {
        release {
        }
    }
    buildFeatures {
        buildConfig = true
    }
    //发版前请务必保证在本地构建完成，否则会像我一样发了个寂寞呜呜呜~
    // to developer
    //发版规则：alphafix->betafix 用于修复已知问题
    //版本号格式：主版本号.次版本号.补丁版本号-(alpha->beta->release |debug) 用于开发者调试|依次递进
    //github发布前务必确认当前版本号是否符合发版规则,以免造成困扰
    def major = 0          // 主版本号
    def minor = 2          // 次版本号
    def patch = 5          // 补丁版本号
    def buildTag = "beta.fix" // 构建标签 (alpha, beta, release, 等)
    def buildDate
    def buildDate2
    def buildTime
    def buildTime2
    def buildTargetCode
    def _versionCode
    def _versionName
    try {
        // 获取当前日期并格式化为 "yyyy-MM-dd" 构建日期
        def dateFormat = new SimpleDateFormat("yy-MM-dd")
        buildDate = dateFormat.format(new Date())
        buildDate2 = dateFormat.format(new Date()).replace("-", ".")
        // 获取当前时间并格式化为 "HH:mm:ss" 作为构建时间，此处时间只用于app展示
        def timeFormat = new SimpleDateFormat("HH:mm:ss")
        buildTime = timeFormat.format(new Date())
        buildTime2 = timeFormat.format(new Date()).replace(":", ".")
        // 使用 MD5 哈希算法生成构建号 (前8位)
        MessageDigest md = MessageDigest.getInstance("MD5")
        md.update(buildTime.bytes)
        buildTargetCode = md.digest().encodeHex().toString().substring(0, 4)
    }
    catch (Exception e) {
        // 错误处理机制
        println("时间或日期格式化错误: ${e.message}")
        buildDate = "unknown"
        buildTime = "unknown"
        buildTime2 = "unknown"
        buildDate2 = "unknown"
        buildTargetCode = "0000"
    }
    // 使用 Git 提交次数生成版本号
    _versionCode = 'git rev-list --count HEAD'.execute().text.trim().toInteger()
    // 应用版本信息，用于展示在LSP
    if (buildTag.contains("alpha")||buildTag.contains("beta")) {
        _versionName = "${major}.${minor}.${patch}-${buildTag}"
    } else if (buildTag == "debug") {
        _versionName = "${buildTargetCode}-${buildTag}"
    } else {
        _versionName = "${major}.${minor}.${patch}-${buildTag}"
        buildTag = ""
    }

    // 配置 Android 构建设置
    namespace "fansirsqi.xposed.sesame" // 命名空间
    compileSdk 34    // 编译SDK版本
    defaultConfig {
        minSdk 26 // 最低SDK版本
        //noinspection OldTargetApi,ExpiredTargetSdkVersion
        targetSdk 34 // 目标SDK版本
        versionCode _versionCode  // 设置版本号
        versionName _versionName  // 设置版本名称 (使用 SemVer 格式)
        // 使用 buildConfigField 添加自定义字段 for show ViewInfo.class of BuildConfig.class
        buildConfigField "String", "BUILD_DATE", "\"${buildDate}\"" // 创建常量
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\"" // 创建常量
        buildConfigField "String", "BUILD_NUMBER", "\"${buildTargetCode}\"" // 创建常量
        buildConfigField "String", "BUILD_TAG", "\"${buildTag}\"" // 创建常量
    }

    ext {
        applicationName = "Sesame" // 应用程序名称
        applicationType = "Normal" // 应用程序类型
    }
    flavorDimensions.add("default") // 添加产品风味维度
    productFlavors {
        normal {
            dimension "default" // 普通产品风味
            ext.applicationType = "Normal"
            minSdk 27    // 适用于较新的 Android 版本8
            targetSdk 34
        }
        compatible {
            dimension "default" // 兼容产品风味
            ext.applicationType = "Compatible"
            minSdk 21    // 支持较老的 Android 版本
            targetSdk 27
        }
    }

    signingConfigs {
        config {
            storeFile file('./byseven.jks')
            storePassword project.properties.MY_APP_STORE_PASSWORD
            keyAlias project.properties.MY_APP_KEY_ALIAS
            keyPassword project.properties.MY_APP_KEY_PASSWORD
        }
        debug {

        }
    }

    buildTypes {
        debug {
            debuggable true // 默认为true，可以显式设置
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false // 默认为false，可以显式设置
            minifyEnabled false // 启用代码压缩
            shrinkResources false // 启用资源压缩
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug //for debug apk
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }
    // 构建工具版本
    applicationVariants.configureEach { variant -> // 配置每个应用变体
        variant.outputs.configureEach { output -> // 配置每个输出
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (variant.buildType.name == 'release') {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}.apk"
                }
                if (variant.buildType.name == 'debug') {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}-debug.${buildTargetCode}.apk"
                }
            }
        }
    }

}

// 依赖项配置
dependencies {
    //noinspection GradleDependency
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'

    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'com.google.android.material:material:1.12.0'

    compileOnly 'de.robv.android.xposed:api:82'
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.36' // Lombok 库
    annotationProcessor 'org.projectlombok:lombok:1.18.36' // Lombok 注解处理器
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.18.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.18.1'
}
